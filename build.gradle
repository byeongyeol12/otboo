plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.1'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'jacoco'
}

group = 'com.codeit'
version = '0.0.1-SNAPSHOT'

def querydslDir = "$buildDir/generated/querydsl"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot Starters
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Database
    runtimeOnly 'org.postgresql:postgresql'
    implementation 'io.hypersistence:hypersistence-utils-hibernate-63:3.7.0'

    // QueryDSL
    implementation 'com.querydsl:querydsl-jpa:5.1.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:5.1.0:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // MapStruct
    implementation 'org.mapstruct:mapstruct:1.5.3.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // Others
    implementation 'io.github.cdimascio:dotenv-java:3.0.0'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
    implementation 'org.springframework.security:spring-security-messaging'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'

//	// AWS
    implementation platform("io.awspring.cloud:spring-cloud-aws-dependencies:3.3.0")
    implementation 'io.awspring.cloud:spring-cloud-aws-s3'
//	implementation 'io.awspring.cloud:spring-cloud-aws-starter-secrets-manager'
//	implementation 'io.awspring.cloud:spring-cloud-aws-starter-parameter-store'

    // Test Dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.batch:spring-batch-test'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    //testImplementation 'com.h2database:h2'

    def testcontainersVersion = "1.19.8"
    testImplementation "org.testcontainers:postgresql:${testcontainersVersion}"
    testImplementation "org.testcontainers:junit-jupiter:${testcontainersVersion}"
}

// QueryDSL generated sources
tasks.withType(JavaCompile).configureEach {
    options.generatedSourceOutputDirectory.set(file(querydslDir))
}
sourceSets {
    main.java.srcDirs += [querydslDir]
}

// clean 시 QueryDSL 소스 삭제
clean {
    delete file(querydslDir)
}

// Jacoco 설정
jacoco {
    toolVersion = "0.8.12"
}

// 테스트 실행과 동시에 리포트 생성
tasks.named('test', Test) {
    useJUnitPlatform()
    ignoreFailures = true
    finalizedBy 'jacocoTestReport'
}

// 기존에 생성된 jacocoTestReport 태스크를 커스터마이징
tasks.named('jacocoTestReport', JacocoReport) {
    dependsOn tasks.named('test')

    // 실행 데이터 위치
    executionData.setFrom fileTree(buildDir).include('jacoco/*.exec', 'jacoco/*.exec')

    // 소스 디렉토리
    sourceDirectories.setFrom files('src/main/java')

    // 클래스 디렉토리 (테스트 클래스와 Spring Boot 애플리케이션 클래스를 제외)
    classDirectories.setFrom files([
            fileTree(dir: "$buildDir/classes/java/main", exclude: [
                    '**/*Test.*',
                    '**/application*.class'
            ]),
            // QueryDSL 생성 클래스도 포함
            fileTree(dir: querydslDir, include: '**/*.class')
    ])

    reports {
        html.required.set(true)
        html.outputLocation.set(layout.buildDirectory.dir('reports/jacoco/html'))
        xml.required.set(false)
        csv.required.set(false)
    }
}
